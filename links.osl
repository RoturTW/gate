def getLink(string id) object (
    string linkPath = "./db/links/" + id + ".json"
    if !fs.Exists(linkPath) (
        return { ok: false, error: "not found" }
    )

    string jsonStr = fs.ReadFile(linkPath)
    any parsed = jsonStr.JsonParse()
    if typeof(parsed) != "object" (
        return { ok: false, error: "invalid" }
    )

    return { ok: true, link: parsed }
)

def linkExists(string id) boolean (
    string linkPath = "./db/links/" + id + ".json"
    return fs.Exists(linkPath)
)

def generateLinkId() string (
    string id = randomString(7)
    while linkExists(id) (
        id = randomString(7)
    )
    return id
)

def createLink(string id, string to, string owner) object (
    string linkPath = "./db/links/" + id + ".json"
    if fs.Exists(linkPath) (
        return { ok: false, error: "already exists" }
    )
    if to.len > 2000 (
        return { ok: false, error: "url is too long, keep under 2000 characters" }
    )
    object linkData = {
        "to": to,
        "owner": owner,
        "created_at": time.Now().Unix(),
        "id": id
    }
    string resp = ownerAddLink(id, owner)
    if resp != "" (
        return { ok: false, error: resp }
    )
    fs.WriteFile(linkPath, linkData.toStr())
    return { ok: true, link: linkData }
)

def deleteLink(string id) object (
    string linkPath = "./db/links/" + id + ".json"
    if !fs.Exists(linkPath) (
        return { ok: false, error: "link not found" }
    )
    string owner = getLink(id).owner
    string resp = ownerRemoveLink(id, owner)
    if resp != "" (
        return { ok: false, error: resp }
    )
    fs.Remove(linkPath)
    return { ok: true }
)

def ownerGetLinks(string owner) array (
    string ownerPath = "./db/users/" + owner + ".json"
    if !fs.Exists(ownerPath) (
        return []
    )
    string jsonStr = fs.ReadFile(ownerPath)
    any parsed = jsonStr.JsonParse()
    if typeof(parsed) != "array" (
        return []
    )
    return parsed.assert(array)
)

def ownerSetLinks(string owner, array links) string (
    string ownerPath = "./db/users/" + owner + ".json"
    if links.len >= 100 (
        return "max links reached"
    )
    fs.WriteFile(ownerPath, links.toStr())
    return ""
)

def ownerAddLink(string id, string owner) string (
    array links = ownerGetLinks(owner)
    if links.contains(id) (
        return "link already exists"
    )
    void links.append(id)
    return ownerSetLinks(owner, links)
)

def ownerRemoveLink(string id, string owner) string (
    array links = ownerGetLinks(owner)
    if !links.contains(id) (
        return "link not found"
    )
    array newLinks = []
    for i links.len (
        if links[i] == id (
            continue
        )
        void newLinks.append(links[i])
    )
    return ownerSetLinks(owner, newLinks)
)
    