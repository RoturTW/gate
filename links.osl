def getLink(string id) object (
    string linkPath = "./db/links/" + id + ".json"
    if !fs.Exists(linkPath) (
        return { ok: false, error: "not found" }
    )

    string jsonStr = fs.ReadFile(linkPath)
    any parsed = jsonStr.JsonParse()
    if typeof(parsed) != "object" (
        return { ok: false, error: "invalid" }
    )

    return { ok: true, link: parsed }
)

def addLinkView(string id) string (
    object link = getLink(id)
    if !link.ok (
        return "link not found"
    )
    object parsed = link.link
    parsed.views = parsed.views.toNum() + 1
    string linkPath = "./db/links/" + id + ".json"
    fs.WriteFile(linkPath, parsed.toStr())
    return ""
)

def linkExists(string id) boolean (
    string linkPath = "./db/links/" + id + ".json"
    return fs.Exists(linkPath)
)

def generateLinkId() string (
    string id = randomString(7)
    while linkExists(id) (
        id = randomString(7)
    )
    return id
)

def createLink(string id, string to, string owner) object (
    string linkPath = "./db/links/" + id + ".json"
    if fs.Exists(linkPath) (
        return { ok: false, error: "already exists" }
    )
    if to.len > 2000 (
        return { ok: false, error: "url is too long, keep under 2000 characters" }
    )
    object linkData = {
        "to": to,
        "owner": owner,
        "created_at": time.Now().Unix(),
        "id": id,
        "views": 0
    }
    string resp = ownerAddLink(id, owner)
    if resp != "" (
        return { ok: false, error: resp }
    )
    fs.WriteFile(linkPath, linkData.toStr())
    return { ok: true, link: linkData }
)

def renameLink(string id, string newName) object (
    string linkPath = "./db/links/" + id + ".json"
    string newLinkPath = "./db/links/" + newName + ".json"
    if !fs.Exists(linkPath) (
        return { ok: false, error: "link not found" }
    )
    if fs.Exists(newLinkPath) (
        return { ok: false, error: "link with that name already exists" }
    )
    object link = getLink(id).link
    string owner = link.owner
    string resp = ownerRenameLink(id, newName, owner)
    if resp != "" (
        return { ok: false, error: resp }
    )
    
    link.id = newName
    fs.WriteFile(newLinkPath, link.toStr())
    fs.Remove(linkPath)
    return { ok: true }
)

def deleteLink(string id) object (
    string linkPath = "./db/links/" + id + ".json"
    if !fs.Exists(linkPath) (
        return { ok: false, error: "link not found" }
    )
    string owner = getLink(id).link.owner
    string resp = ownerRemoveLink(id, owner)
    if resp != "" (
        return { ok: false, error: resp }
    )
    fs.Remove(linkPath)
    return { ok: true }
)

def ownerGetLinks(string owner) array (
    string ownerPath = "./db/users/" + owner + ".json"
    if !fs.Exists(ownerPath) (
        return []
    )
    string jsonStr = fs.ReadFile(ownerPath)
    any parsed = jsonStr.JsonParse()
    if typeof(parsed) != "array" (
        return []
    )
    return parsed.assert(array)
)

def ownerSetLinks(string owner, array links) string (
    string ownerPath = "./db/users/" + owner + ".json"
    if links.len >= 100 (
        return "max links reached"
    )
    fs.WriteFile(ownerPath, links.toStr())
    return ""
)

def ownerAddLink(string id, string owner) string (
    array links = ownerGetLinks(owner)
    if links.contains(id) (
        return "link already exists"
    )
    void links.append(id)
    return ownerSetLinks(owner, links)
)

def ownerRenameLink(string id, string newName, string owner) string (
    object user = userData[owner.toLower()]
    string subscription = user.subscription.toStr().toLower()

    if subscription == "free" or subscription == "lite" (
        return "link rename not allowed for your subscription"
    )

    array links = ownerGetLinks(owner)
    if !links.contains(id) (
        return "link not found"
    )
    for i links.len (
        if links[i] == id (
            links[i] = newName
            break
        )
    )
    return ownerSetLinks(owner, links)
)

def ownerRemoveLink(string id, string owner) string (
    array links = ownerGetLinks(owner)
    if !links.contains(id) (
        return "link not found"
    )
    array newLinks = []
    for i links.len (
        if links[i] == id (
            continue
        )
        void newLinks.append(links[i])
    )
    return ownerSetLinks(owner, newLinks)
)
    