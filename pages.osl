
def handleAuth(*gin.Context c) (
    string token = c.Query("v")
    if token == "" (
        c.JSON(401, { ok: false, error: "missing token" })
        return
    )
    object resp = requests.Get("https://api.rotur.dev/validate?key=rotur-gate&v=" + token)

    if !resp.success (
        c.JSON(401, { ok: false, error: "failed to validate token" })
        return
    )

    object json = resp.body.toStr().JsonParse()
    if json.error != null (
        c.JSON(401, { ok: false, error: json.error })
        return
    )
    if !json.valid (
        c.JSON(401, { ok: false, error: "invalidate token" })
        return
    )

    string sessionId = randomString(32)
    
    string username = token.split(",")[1]
    sessions[sessionId] = username

    object resp_profile = requests.Get("https://api.rotur.dev/profile?name=" + username)
    if !resp_profile.success (
        c.JSON(401, { ok: false, error: "failed to fetch profile" })
        return
    )
    object profile = resp_profile.body.toStr().JsonParse()
    if profile.error != null (
        c.JSON(401, { ok: false, error: profile.error })
        return
    )
    userData[username.toLower()] = profile

    c.SetCookie("session_id", sessionId, 3600, "/", "", false, true)

    c.JSON(200, { ok: true, token: token })
)

def handleMe(*gin.Context c) (
    string username = c.MustGet("username").toStr()
    object me = userData[username.toLower()]
    object resp = {
        "username": username,
        "canRename": false
    }

    string subscription = me.subscription.toStr().toLower()
    resp.canRename = (subscription == "free" or subscription == "lite") == false
    c.JSON(200, resp)
)

def handleMyLinks(*gin.Context c) (
    string username = c.MustGet("username").toStr()
    array linkiDs = ownerGetLinks(username)
    array out = []
    for i linkiDs.len (
        void out.append(getLink(linkiDs[i].toStr()).link)
    )
    c.JSON(200, out)
)

def handleCreateLink(*gin.Context c) (
    string username = c.MustGet("username").toStr()
    string id = generateLinkId()
    string to = c.Query("to")

    string err = validateDestination(to)
    if err != "" (
        c.JSON(400, { ok: false, error: err })
        return
    )
    object resp = createLink(id, to, username)
    if !resp.ok (
        c.JSON(400, { ok: false, error: resp })
        return
    )
    c.JSON(200, resp.link)
)

def handleDeleteLink(*gin.Context c) (
    string username = c.MustGet("username").toStr()
    string id = c.Query("id")
    if id == "" (
        c.JSON(400, { ok: false, error: "missing id" })
        return
    )
    object res = getLink(id)
    if !res.ok (
        c.JSON(404, { ok: false, error: "not found" })
        return
    )
    object link = res.link
    if link.owner.toStr() != username (
        c.JSON(403, { ok: false, error: "not authorized" })
        return
    )
    object resp = deleteLink(id)
    if !resp.ok (
        c.JSON(400, { ok: false, error: resp.error })
        return
    )
    c.JSON(200, { ok: true })
)

def handleRenameLink(*gin.Context c) (
    string username = c.MustGet("username").toStr()
    string id = c.Query("id")
    string newName = c.Query("newName")
    if id == "" (
        c.JSON(400, { ok: false, error: "missing id" })
        return
    )
    if newName == "" (
        c.JSON(400, { ok: false, error: "missing new name" })
        return
    )

    if newName.len > 100 (
        c.JSON(400, { ok: false, error: "new name is too long" })
        return
    )

    object res = getLink(id)
    if !res.ok (
        c.JSON(404, { ok: false, error: "not found" })
        return
    )
    object link = res.link
    if link.owner.toStr() != username (
        c.JSON(403, { ok: false, error: "not authorized" })
        return
    )

    object resp = renameLink(id, newName)
    if !resp.ok (
        c.JSON(400, { ok: false, error: resp.error })
        return
    )
    c.JSON(200, { ok: true })
)

def handleId(*gin.Context c) (
    string slug = c.Param("id")
    if slug == "" (
        c.String(404, "not found")
        return
    )

    if slug.endsWith(".json") (
        string id = slug.trim(1, -6)
        if id == "" (
            c.JSON(404, { ok: false, error: "not found" })
            return
        )
        object res = getLink(id)
        if !res.ok (
            c.JSON(404, { ok: false, error: "not found" })
            return
        )
        c.JSON(200, res.link)
        return
    )

    object res = getLink(slug)
    if res.ok != true (
        c.String(404, "not found")
        return
    )
    object link = res.link
    
    addLinkView(slug)
    c.Redirect(302, link.to.toStr())
)

def authPage(*gin.Context c) (
    c.HTML(200, "auth.html", { AuthKey: authKey })
)

def dashboardPage(*gin.Context c) (
    string username = c.MustGet("username").toStr()
    object user = userData[username.toLower()]
    c.HTML(200, "index.html", { Username: username, Subscription: user.subscription.toStr() })
    return
)